import org.apache.tools.ant.taskdefs.condition.Os

boolean hasAndroidSdk() {
	try {
		def androidHome = "$sdk.dir"
		if(androidHome != null && androidHome.length() > 0) {
			return true;
		}
	} catch (all) {
		println all
	}
	try {
		def androidHome = "$System.env.ANDROID_HOME"
		if(androidHome != null && androidHome.length() > 0) {
			return true;
		}
	} catch (all) {
		println all
	}
	
	try {
		def androidExecPath = new ByteArrayOutputStream()
		if (Os.isFamily(Os.FAMILY_WINDOWS)) {
			exec {
				commandLine 'where', 'android'
				standardOutput = androidExecPath
			}
		} else {
			exec {
				commandLine 'which', 'android'
				standardOutput = androidExecPath
			}
		}
		return androidExecPath.toString() != null && androidExecPath.toString().trim().length() > 0;
	} catch (all) {
		println all
	}
	return false
}

include "core", "desktop", "tiled", "artemis-odb", "uats", "uats-desktop"

if (hasAndroidSdk()) {
	include "android", "uats-android"
}

if (Os.isFamily(Os.FAMILY_MAC)) {
	include "ios", "uats-ios"
}

rootProject.name = 'mini2Dx'
rootProject.children.each { it.name = rootProject.name + "-" + it.name }